/*
 * Copyright 2016 SimplifyOps, Inc. (http://simplifyops.com)
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import java.io.File;
import java.util.Map;

import org.gradle.api.Project;
import org.gradle.api.DefaultTask
import org.gradle.api.tasks.TaskAction
import org.gradle.plugins.signing.Sign

import java.util.jar.JarInputStream

/**
 * The Rundeck webapp build file
 *
 * It's important to note that grails has it's own build system based on Gant. Rather
 * than trying to integrate Gradle directly with Gant or use one of the Gradle-Grails plugins
 * available, we're following in the steps of the Make based build and just wrapping 
 * around the Grails builds.
 */

description = 'The Rundeck Grails webapp project'

buildscript {
    repositories {
        mavenLocal()
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsVersion"
        classpath "com.bertramlabs.plugins:asset-pipeline-gradle:2.14.1"
        classpath 'com.bertramlabs.plugins:less-asset-pipeline:2.7.2'
        classpath "org.grails.plugins:hibernate5:${gormVersion-".RELEASE"}"
    }
}

version "0.1"
group "rundeckapp"

apply plugin:"eclipse"
apply plugin:"idea"
apply plugin:"war"
apply plugin:"org.grails.grails-web"
apply plugin:"org.grails.grails-gsp"
apply plugin:"asset-pipeline"
apply plugin:"maven"
apply plugin:"signing"
apply plugin:"com.google.osdetector"


// set the convention to rundeck:<branch>:<path>:<projectName>
eclipse.project.name =  "${project.getParent().eclipse.project.name}:webapp"

// Properties for downloading, installing and running Grails
def grailsBaseName = "grails-${grailsVersion}"
def grailsZipFile = "${grailsBaseName}.zip"
def grailsZipFileDefaultBasePath = "${rootProject.projectDir}/local/tmp"
def boolean grailsZipFileBasePathOverriden = hasProperty("grailsZipFileBasePath")
def grailsZipFileBasePath = grailsZipFileBasePathOverriden ? grailsZipFileBasePath : grailsZipFileDefaultBasePath
def grailsZipFileLocation = "${grailsZipFileBasePath}/${grailsZipFile}"
def grailsInstallLocation = "${rootProject.projectDir}/local"
def grailsLocalRepo = hasProperty('grailsLocalRepo') ? grailsLocalRepo : 'grails-app/plugins'
def grailsCentral = hasProperty('disableGrailsCentral') && disableGrailsCentral=='true' ? "-Ddisable.grails.central=${disableGrailsCentral}" : "-Dgrails.central.url=${grailsCentralUrl}"
//def grailsDownloadUrl = "http://dist.springframework.org.s3.amazonaws.com/release/GRAILS/grails-${grailsVersion}.zip"
def grailsDownloadUrl = "https://github.com/grails/grails-core/releases/download/v${grailsVersion}/grails-${grailsVersion}.zip"
ext.grailsHome = "${grailsInstallLocation}/${grailsBaseName}"
def grailsCommandLine() { if (osdetector.os.contains("windows")) { return "${grailsHome}/bin/grails.bat" } else { "${grailsHome}/bin/grails" } }
def mavenCredsDefined = hasProperty('mavenUser') && hasProperty('mavenPassword') && hasProperty('mavenRealm') && hasProperty('mavenHost')
def mavenCreds = mavenCredsDefined ? "-Dmaven.user=${mavenUser} -Dmaven.password=${mavenPassword} -Dmaven.realm=${mavenRealm} -Dmaven.host=${mavenHost}" : ''
def warFileName = "rundeck-${version}.war"
def warFileLocation = "${project.projectDir}/target/${warFileName}"
def additionalFileLocation = "${project.projectDir}/target/war-contents"

//BuildConfig configurations --start
def mavenCentralUrl = 'http://repo1.maven.org/maven2/'
if (System.getProperty('maven.central.url')) {
    mavenCentralUrl = System.getProperty('maven.central.url')
}



//BuildConfig configurations --end

configurations{
    pluginFiles {
        transitive = false

    }
    pluginFiles.extendsFrom(runtime)
}
allprojects {
    repositories {
        maven { url "https://jitpack.io" }
    }
}

repositories {
    mavenLocal()
    maven { url "https://repo.grails.org/grails/core" }
}
/*
plugins {
    test    ':code-coverage:2.0.3-3'
    compile (':less-asset-pipeline:2.7.2')
    compile ':twitter-bootstrap:3.3.2.1'
//    compile (':asset-pipeline:2.7.2')
    compile ':cache:1.1.8'
    compile ":platform-core:1.0.0"
//    runtime (':hibernate4:4.3.10')
    runtime ':mail:1.0.7', ':quartz:1.0.2', ':executor:0.3'

    runtime ':profiler:0.5'
    runtime ':miniprofiler:0.4.1'
    provided ':codenarc:0.22'
    build   ':jetty:3.0.0'
}*/

dependencies {
    compile "org.springframework.boot:spring-boot-starter-logging"
    compile "org.springframework.boot:spring-boot-autoconfigure"
    compile "org.grails:grails-core"
    compile "org.springframework.boot:spring-boot-starter-actuator"
//    compile "org.springframework.boot:spring-boot-starter-tomcat"
    compile "org.springframework.boot:spring-boot-starter-jetty"
    compile 'org.eclipse.jetty:jetty-jaas:9.0.0.v20130308'
    compile "org.grails:grails-dependencies"
    compile "org.grails:grails-web-boot"
    compile "org.grails.plugins:cache"
    compile "org.grails.plugins:scaffolding"
    compile "org.grails.plugins:hibernate5"
    compile "org.hibernate:hibernate-core:5.1.3.Final"
    compile "org.hibernate:hibernate-ehcache:5.1.3.Final"
    compile "com.google.guava:guava:15.0"
    console "org.grails:grails-console"
    profile "org.grails.profiles:web"
    runtime "com.bertramlabs.plugins:asset-pipeline-grails:2.14.1"
    //less plugin
    assets "com.bertramlabs.plugins:less-asset-pipeline:2.7.2"
    runtime "com.h2database:h2"
    testCompile "org.grails:grails-plugin-testing"
    testCompile "org.grails.plugins:geb"
    testRuntime "org.seleniumhq.selenium:selenium-htmlunit-driver:2.47.1"
    testRuntime "net.sourceforge.htmlunit:htmlunit:2.18"

    //BuildConfig dependencies
//    build 'org.yaml:snakeyaml:1.9'
//    compile 'org.yaml:snakeyaml:1.9',
    compile 'org.apache.ant:ant:1.8.3',
            'org.apache.ant:ant-jsch:1.8.3',
            'com.jcraft:jsch:0.1.54',
            'log4j:log4j:1.2.17',
            'commons-collections:commons-collections:3.2.2',
            'commons-codec:commons-codec:1.5',
            'com.fasterxml.jackson.core:jackson-databind:2.8.8.1',
            'com.fasterxml.jackson.core:jackson-annotations:2.8.8',
            'com.codahale.metrics:metrics-core:3.0.1',
            'com.google.guava:guava:15.0',
            'org.owasp.encoder:encoder:1.2',
            'org.quartz-scheduler:quartz:2.2.1',
            'org.markdownj:markdownj-core:0.4',
            'com.atlassian.commonmark:commonmark:0.9.0',
            'com.atlassian.commonmark:commonmark-ext-gfm-tables:0.9.0',
            'com.googlecode.owasp-java-html-sanitizer:owasp-java-html-sanitizer:20160614.1'

    //profiler plugin
    compile "org.grails.plugins:profiler:0.5"

    //spring security
    compile("org.springframework.boot:spring-boot-starter-security")

    //quartz plugin
    compile 'org.grails.plugins:quartz:2.0.1'

    runtime 'org.postgresql:postgresql:42.0.0'
    runtime 'mysql:mysql-connector-java:5.1.35'

    //BEGIN fix hibernate4 bug with dateCreated auto timestamp, see: https://jira.grails.org/browse/GPHIB-30
    compile "javax.validation:validation-api:1.1.0.Final"
    runtime "org.hibernate:hibernate-validator:5.0.3.Final"
    //END fix for https://jira.grails.org/browse/GPHIB-30
    runtime 'com.h2database:h2:1.4.193'

    compile "org.grails.plugins:platform-core:1.0.0"

    //plugin for the filters
    compile 'org.grails:grails-plugin-filters:3.0.12'

    //end


    compile project(":core")
    compile project(":rundeck-storage")
//    compile project(":rundeckapp:metricsweb")
//    compile project(":rundeck-launcher")

    pluginFiles project(':plugins:script-plugin'),
            project(':plugins:stub-plugin'),
            project(':plugins:localexec-plugin'),
            project(':plugins:copyfile-plugin'),
            project(':plugins:job-state-plugin'),
            project(':plugins:flow-control-plugin'),
            project(':plugins:jasypt-encryption-plugin'),
            project(':plugins:orchestrator-plugin'),
            project(':plugins:git-plugin'),
            project(':plugins:source-refresh-plugin'),
            "com.github.Batix:rundeck-ansible-plugin:2.0.2"
}

grails {
    plugins {
        compile project(":rundeckapp:metricsweb")
        compile project(":rundeckapp:Webrealms")
    }
}


task('copyPluginLibs').dependsOn(configurations.pluginFiles).doLast {
    File libextDir = new File(additionalFileLocation, "WEB-INF/rundeck/plugins")
    File manifestFile = new File(additionalFileLocation, "WEB-INF/rundeck/plugins/manifest.properties")

    def filelist=[]
    libextDir.mkdirs()
    configurations.pluginFiles.files.each { pluginFile ->
        copy {
            from pluginFile
            into libextDir
        }
        filelist << pluginFile.name
        //load jar manifest
        def pluginProps = [:]

        pluginFile.withInputStream {
            def jarmf=new JarInputStream(it).manifest
            jarmf.getMainAttributes().findAll{it.key.toString().startsWith('Rundeck-Plugin')}.each{k,v->
                pluginProps[k.toString()]=v
            }
        }
        def f= pluginFile.name
        Properties fileProps = new Properties()
        fileProps[  'plugin.name']= pluginProps['Rundeck-Plugin-Name']?:f
        fileProps[  'plugin.description']= pluginProps['Rundeck-Plugin-Description'] ?:'Rundeck bundled plugin'
        fileProps[  'plugin.author']= pluginProps['Rundeck-Plugin-Author'] ?:'Rundeck, Inc.'
        fileProps[  'plugin.version']= pluginProps['Rundeck-Plugin-File-Version'] ?:version
        fileProps[  'plugin.url']= pluginProps['Rundeck-Plugin-URL'] ?:'http://rundeck.com'
        fileProps[  'plugin.date']= pluginProps['Rundeck-Plugin-BuildDate'] ?:(new Date().toString())
        fileProps[  'plugin.filename']= f

        //write properties file
        new File(libextDir,pluginFile.name+'.properties').withOutputStream {
            fileProps.store(it,"generated manifest")
        }
    }
    Properties manifestProps = new Properties()
    manifestProps['pluginFileList'] = (filelist.join(','))
    manifestFile.withWriter { w ->
        manifestProps.store(w,"generated manifest")
    }
}
/**
 * Downloads Grails from the SpringSource archives
 */
task downloadGrails {
    File grailsArchive = file(grailsZipFileLocation);
    outputs.file grailsArchive;
    doLast {
        if (grailsZipFileBasePathOverriden) {
            logger.warn("Using existing Grails zip file: ${grailsZipFileLocation}");
            if (!grailsArchive.isFile()) {
                throw new InvalidUserDataException("Grails zip file does not exist: ${grailsZipFileLocation}");
            }
        } else {
            logger.warn("Downloading ${grailsBaseName}...");
            file("${grailsZipFileBasePath}").mkdirs();
            if (!grailsArchive.isFile()) {
                ant.get(src: grailsDownloadUrl, dest: grailsZipFileLocation, verbose: true);
            }
        }
    }
}

/**
 * Expand the downloaded archive if it hasn't already been expanded
 */
task extractGrails(dependsOn: downloadGrails) {
    logger.debug("grailsZipFileLocation: ${grailsZipFileLocation}")
    inputs.file file(grailsZipFileLocation);

    logger.debug("grailsInstallLocation: ${grailsInstallLocation}")
    outputs.dir file(grailsHome);

    doLast {
        logger.info("Extracting ${grailsZipFile}...")
        file(grailsInstallLocation).mkdirs();
        copy {
            from zipTree(grailsZipFileLocation);
            into file(grailsInstallLocation);
        }
        // make sure that everything but the .bat files are set +x on linux / macs
        if (System.getProperty('os.name').toLowerCase().indexOf('win') < 0) {
            FileTree tree = fileTree("${grailsInstallLocation}/${grailsBaseName}/bin").exclude('**/*.bat');
            tree.each { File file ->
                logger.info('Setting +x permission on ' + file);
                file.setExecutable(true);
            }
        }
    }
}

def projDeps = [
        ":core",
        ":rundeck-storage:rundeck-storage-api",
        ":rundeck-storage:rundeck-storage-data",
        ":rundeck-storage:rundeck-storage-filesys",
        ":rundeck-storage:rundeck-storage-conf",
        ":rundeck-launcher:rundeck-jetty-server"
].collect{
    project(it).install
}
/**
 * "core" needs to be built and installed into the local ~/.m2 repo before
 * we attempt to do anything further with Grails.
 */
task('installDependencies').dependsOn(projDeps).doLast {
    description = "Builds and installs dependencies on other subprojects"
}

/**
 * Installs the jetty-plugin into the .grails folder
 */
task installJettyPlugin(type: Exec, dependsOn: [extractGrails]) {
    def jettyPluginVersion = '2.0.3'
    def jettyPluginInstalled = "${System.properties['user.home']}/.grails/${grailsVersion}/projects/rundeckapp/plugins/jetty-${jettyPluginVersion}"

    inputs.dir file(grailsHome)
    outputs.dir file(jettyPluginInstalled)

    workingDir project.projectDir
    environment 'GRAILS_HOME', grailsHome
    commandLine grailsCommandLine()
    args "-Dmaven.central.url=${mavenCentralUrl}",
            "${mavenCreds}",
            "${grailsCentral}",
            "-Dgrails.local.repo=${grailsLocalRepo}",
            "-DRUNDECK_VERSION=${version}",
            'install-plugin',
            'jetty',
            jettyPluginVersion
}



task cleanWar(type: Delete) {
    delete file(warFileLocation), file("${project.projectDir}/target")
}

task cleanGrails(type: Exec, dependsOn: [extractGrails]) {
    inputs.dir file(grailsHome)
    ignoreExitValue=true
    workingDir project.projectDir
    environment 'GRAILS_HOME', grailsHome
    commandLine grailsCommandLine()
    args "-Dmaven.central.url=${mavenCentralUrl}",
            "${mavenCreds}",
            "${grailsCentral}",
            "-Dgrails.local.repo=${grailsLocalRepo}",
            "-DRUNDECK_VERSION=${version}", 'clean'
}

task cleanAll(dependsOn: [cleanEclipse, cleanWar, cleanGrails]) {
}

clean.dependsOn cleanAll

/**
 * Wrapper task for grails test-app
 */
task testGrails(type: Exec, overwrite: true, dependsOn: [installDependencies, extractGrails]) {
    def rdbase=new File(temporaryDir,'rdeck_base')
    rdbase.mkdirs()
    inputs.dir file(grailsHome)
    workingDir project.projectDir
    environment 'GRAILS_HOME', grailsHome
    commandLine grailsCommandLine()
    args "-Dmaven.central.url=${mavenCentralUrl}",
            "${mavenCreds}",
            "${grailsCentral}",
            "-Dgrails.local.repo=${grailsLocalRepo}",
            "-DRUNDECK_VERSION=${version}",
            "-Drdeck.base=${rdbase}",
            'test-app'
}
testGrails.doFirst {
    def rdbase=new File(temporaryDir,'rdeck_base')
    rdbase.mkdirs()

}
test.dependsOn testGrails


/**
 * Run grails compile
 */
task grailsCompile(type: Exec, overwrite: true, dependsOn: [extractGrails]) {
    inputs.dir file(grailsHome)
    workingDir project.projectDir
    environment 'GRAILS_HOME', grailsHome
    commandLine grailsCommandLine()
    args "-Dmaven.central.url=${mavenCentralUrl}",
            "${mavenCreds}",
            "${grailsCentral}",
            "-Dgrails.local.repo=${grailsLocalRepo}",
            "-DRUNDECK_VERSION=${version}",
            'prod',
            'compile'
}
/**
 * Builds the rundeck war file
 */
task grailsWar(type: Exec, overwrite:true, dependsOn: [ installDependencies,extractGrails, copyPluginLibs]) {
    inputs.dir file(grailsHome)
    inputs.dir(file("${projectDir}/src")).skipWhenEmpty()
    outputs.file file(warFileLocation)
    workingDir project.projectDir
    environment 'GRAILS_HOME', grailsHome
    commandLine grailsCommandLine()
    args "-Dmaven.central.url=${mavenCentralUrl}",
            "${mavenCreds}",
            "${grailsCentral}",
            "-Dgrails.local.repo=${grailsLocalRepo}",
            "-Drundeck.war.additional=${additionalFileLocation}",
            "-DRUNDECK_VERSION=${version}",
            'prod',
            'war',
            '--non-interactive',
            warFileLocation
}

// add the war to default configuration for this project
artifacts{
    'default'(file(warFileLocation)){
        name 'rundeck'
        type 'war'
        builtBy grailsWar
    }
}

task buildAll(overwrite: true, dependsOn: [test, grailsWar]) {
    // noop build because this isn't actually a java project
    // besides the war task is handling building by issuing a "grails war"
}

build.dependsOn buildAll

//********* artifact signing *********
if(isReleaseBuild && project.hasProperty("signing.keyId")) {
    signing {
        sign configurations.archives
    }
} else {
    task signArchives {
        // do nothing
    }
}

//build a pom we reuse for both maven builds and release to sonatype
def deploypom=pom {
    project {
        artifactId archivesBaseName
        groupId project.group
        inceptionYear '2011'
        packaging 'war'
        version version
        name "Rundeck Web app"
        description "Rundeck web console for command dispatching and job scheduling"
        url 'http://rundeck.org'
        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
            }
        }
        properties{
            'version'(version)
        }
        scm {
            url 'https://github.com/dtolabs/rundeck'
            connection 'scm:git:git@github.com/dtolabs/rundeck.git'
            developerConnection 'scm:git:git@github.com:dtolabs/rundeck.git'
        }
        developers {
            developer {
                id('gschueler')
                name('Greg Schueler')
                email('greg@dtosolutions.com')
            }
        }
        parent {
            groupId('org.sonatype.oss')
            artifactId('oss-parent')
            version('7')
        }
    }
}
// prompt for PGP key passphrase if not set
gradle.taskGraph.whenReady { taskGraph ->
    if (taskGraph.allTasks.any { it instanceof Sign } && project.hasProperty("signing.keyId") && !project.hasProperty("signing.password") && !isDevBuild) {
        // Use Java 6's console to read from the console (no good for a CI environment)
        Console console = System.console()
        console.printf "\n\nWe have to sign some things in this build.\n\nPlease enter your signing details.\n\n"

        //def id = console.readLine("PGP Key Id: ")
        //def file = console.readLine("PGP Secret Key Ring File (absolute path): ")
        def password = console.readPassword("PGP Private Key Password: ")

        //allprojects { ext."signing.keyId" = id }
        //allprojects { ext."signing.secretKeyRingFile" = file }
        allprojects { ext."signing.password" = password }

        console.printf "\nThanks.\n\n"
    }
}

uploadArchives {
    if(isDevBuild){
        repositories{
            repositories.mavenDeployer {
                configuration = configurations.archives
                pom=deploypom
            }
        }
    }else{
        repositories.mavenDeployer {
            if(isReleaseBuild){
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
            }

            configuration = configurations.archives
            if(project.hasProperty('sonatypeUsername') && project.hasProperty('sonatypePassword')){
                repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2') {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }
                snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots/') {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }
            }
            pom=deploypom
        }
    }
}


bootRun {
    jvmArgs('-Dspring.output.ansi.enabled=always','-Drundeck.config.location=rundeck-config')
    addResources = true
}


assets {
    minifyJs = true
    minifyCss = true
}